 Run port;cl;clt;data;done;drc;dt;ev;host;hp;msg;nm;port;rc;stream
⍝ Connect to LanguageClient on host:port address in LSP_TCP env var
 Init

 :If port>0
     host←'127.0.0.1'
 :Else
     hp←2 ⎕NQ'.' 'GetEnvironment' 'LSP_TCP'
     host port←':'(≠⊆⊢)hp
     port←⊃2⊃⎕VFI port
 :EndIf

 rc clt←2↑DRC.Clt''host port'Raw'
 done←0
 stream←⍬
 cl←0
 :While ~done
     drc←DRC.Wait clt
     rc nm ev dt←4↑drc
     :If rc=0
         stream,←dt
     :ElseIf rc=100
         :Continue
     :Else
         ∘∘∘
     :EndIf
     :If cl=0
         cl stream←CheckContentLength stream
     :EndIf
     :If (cl>0)∧cl≤≢stream
         data←cl↑stream
         stream↓⍨←cl
         Log data
         clt HandleRequest&data
         cl←0
     :EndIf

 :EndWhile

 DRC.Close clt
 ⎕OFF
 ⍝
