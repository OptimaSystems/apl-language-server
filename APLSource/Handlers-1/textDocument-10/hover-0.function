 r←hover req;LS;NL;c;desc;file;fn;ind;line;lines;p;pos;title;uri
 LS←##.##
 r←⊂'null'
 uri←req.params.textDocument.uri
 pos←req.params.position
 file←'file:///'
 :If file(⊃⍷)uri
     fn←LS.PercentDecode(≢file)↓uri
     lines←⊃⎕NGET fn 1
     line←(pos.line+1)⊃lines
     ind←(+\1+65535<⎕UCS line)⍳1+pos.character
 ⍝    ⎕←ind⊃line
     :Trap 3 6
         title desc←LS.LB_HELP.tips.⍎0(7162⌶)ind⊃line
     :Else
         :Return
     :EndTrap
     NL←⎕UCS 10
     r←⎕NS''
     c←r.(contents←⎕NS'')
     c.kind←'markdown'
     c.value←'## ',title,NL
     c.value,←'```apl',NL
     c.value,←desc,NL
     c.value,←'```',NL
     p←r.(range←⎕NS'')
     p.(start←⎕NS'').(line character)←pos.(line character)
     p.(end←⎕NS'').(line character)←pos.(line character)+0,1+65535<⎕UCS ind⊃line

 :EndIf
⍝ ⎕←⎕JSON req
 ⍝
